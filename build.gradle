plugins {
    id "java"
    id "com.gradleup.shadow" version "8.3.0-SNAPSHOT"
}

group = "com.github.kaspiandev"
version = "1"

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "CodeMC"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "minebench-repo"
        url = "https://repo.minebench.de/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "de.tr7zw:item-nbt-api-plugin:2.13.1"

    implementation "de.themoep:inventorygui:1.6.3-SNAPSHOT"
    implementation "de.themoep:minedown:1.7.1-SNAPSHOT"
    implementation "dev.dejvokep:boosted-yaml:1.3.5"
}

shadowJar {
    archiveClassifier.set(null)

    relocate "de.themoep.inventorygui", "com.github.kaspiandev.nbtgui.inventorygui"
    relocate "de.themoep.minedown", "com.github.kaspiandev.nbtgui.minedown"
    relocate "dev.dejvokep.boostedyaml", "com.github.kaspiandev.nbtgui.boostedyaml"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

tasks.build.dependsOn("shadowJar")
